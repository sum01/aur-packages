# Maintainer: sum01 <sum01@protonmail.com>
pkgname=('vcash-cmake-git')
_srcname=('vcash')
pkgver=0.6.0.4.r57.g5806573
pkgrel=1
pkgdesc="A decentralized currency for the internet."
arch=('i686' 'x86_64')
url="https://github.com/openvcash/vcash"
license=('AGPL3')
depends=('db>=5.3.0' 'boost>=1.54.0'  'openssl-1.0')
makedepends=('cmake>=3.1.3' 'make')
conflicts=('vcash')
provides=('vcash')
source=("git+https://github.com/openvcash/vcash.git#branch=cmake-test")
sha512sums=('SKIP')
pkgver(){
  cd "$srcdir/$_srcname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}
build() {
  cd "$srcdir/$_srcname"
  cmake -DOPENSSL_INCLUDE_DIR=/usr/include/openssl-1.0 -DOPENSSL_SSL_LIBRARY=/usr/lib/libssl.so.1.0.0 -DOPENSSL_CRYPTO_LIBRARY=/usr/lib/libcrypto.so.1.0.0
  make
}
package() {
  install -Dsm644 "$srcdir/$_srcname/libcoin.a" "$pkgdir/usr/lib/$_srcname/libcoin.a"
  install -Dsm644 "$srcdir/$_srcname/libdatabase.a" "$pkgdir/usr/lib/$_srcname/libdatabase.a"
  install -Dsm755 "$srcdir/$_srcname/vcashd" "$pkgdir/usr/bin/vcashd"
}
